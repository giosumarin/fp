// using the dll

#r "listFS.dll"
// #r "treeFS.dll"
open FSet

// let's create a set
let oneset = ofList [1..10]

// what? I can't see squat!

// utility to see what's going on, but still return a set
let print ss =
  let xs = toList ss
  printf "result set: %A\n" xs
  ss

let _ = print oneset

let ss = add 3 empty |> add 5 |> add 2 |> add 77 |> print

let ss1 = (add 3 ss) |> print;;

let u = union oneset ss |> print;;

// after, try with other representation



// let's test our Set implementation wrt the Set collection
#r "FsCheck"
open FsCheck

(*
 - a suite of tests, as a list, each with a label to identify the culprit

- note that use of List as a mediation betweeb FSet and Set

- note that use of List.sort to avoid false postives due to FSET not being ordered

*)


let test_set x (xs : int list) ys =
  [
    empty |> toList = (Set.empty |> Set.toList) |@ "empty set"
    isEmpty (ofList xs) = Set.isEmpty (Set.ofList xs) |@ "is empty"
    contains x (ofList xs) = Set.contains x (Set.ofList xs) |@ "contains"
    (add x (ofList xs) |> toList |> List.sort) = (Set.add x (Set.ofList xs) |> Set.toList)  |@ "add"
    (union  (ofList xs) (ofList ys) |> toList |> List.sort) =
       (Set.union  (Set.ofList xs)  (Set.ofList ys)|> Set.toList) |@ "union"
    (ofList xs |> toList |> List.sort ) = (Set.ofList xs |> Set.toList) |@ "list"
    ]

do Check.Quick test_set

let test_fold (s : int list)  seed f =
      foldBack f (ofList s)  seed = Set.foldBack f (Set.ofList s)  seed |@ "fold"

do Check.Quick test_fold


// Let's do something more intersting with this: econding maps for an
// interpreter for arithmetic expressions

type expv =
  | V of string          
  | C of int
  | Sum of expv * expv

// we choose 'a to be (string * int)
type envt = FSet<(string * int)> ;;

// we buind a sample env
let envxyz = ofList [ ("x",1) ; ("y",2) ; ("z", 3)] ;;   

(* Now we have to implement a lookup function in an enviroment,
i.e. the equivalient of

Map.find: 'a -> Map<'a,'b> -> b
  
  given : 'a  and  m : Map<'a,'b>

      Map.find a m  = b  if  m(a) = b  
      
  Raise an exception if a \not\in  Dom(m).

We cannot resonable expect the server side for FSet<'<> to provide us
 with such a function. It would be easy to write it with explicit
 recursion, but we do not have access to the internal representation
 of FSet. So all is lost? No! We have been given the magic tool: fold

*)
let mapfind k env exn =
  FSet.foldBack (fun  (x,v) ys -> if k = x then v else ys) env exn


// Note: exn is a default value to be returned when rather when we do
// not find our key Recall that fold traverses the list till the end,
// so an exception would always be thrown

// e.g.
let v1 = mapfind "x" envxyz 0
let v2 = mapfind "xsss" envxyz 0
  
// and now our interpreter ...

let rec evalv e  env  =
    match e with
    | V x ->  mapfind x env 1000  // calcola env(x)  
    | C n -> n
    | Sum(e1,e2)   -> evalv e1 env  + evalv e2 env 

let vv = evalv  (Sum(Sum ( V "y" , V "z" ),  C 10)) envxyz



